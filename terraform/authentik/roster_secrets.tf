# Generated secrets for the KRG Roster service.
# These are stored in Vault after apply; the .env on krg-prod is populated
# from Vault manually (or via vault-agent in the future — see issue #71).
#
# Vault paths:
#   secret/krg-prod/roster       — db_password, session_secret
#   secret/krg-prod/roster-oidc  — client_id, client_secret (see vault_secrets.tf)
#   secret/krg-prod/roster-ldap  — bind_password (set manually after creating svc_roster in AD)
#
# .env bootstrap on krg-prod (after tofu apply):
#   bao kv get -format=json secret/krg-prod/roster | jq -r '.data.data | to_entries[] | "\(.key | ascii_upcase)=\(.value)"'
#   bao kv get -field=client_secret secret/krg-prod/roster-oidc
#   bao kv get -field=bind_password  secret/krg-prod/roster-ldap

resource "random_password" "roster_db" {
  length  = 32
  special = false
}

resource "random_password" "roster_session" {
  length  = 64
  special = false
}

# LDAP service account password — Terraform generates and stores it so the
# value is in Vault before the svc_roster account is created in Samba AD.
# Create the AD account with:
#   samba-tool user create svc_roster "$(bao kv get -field=bind_password secret/krg-prod/roster-ldap)"
resource "random_password" "roster_ldap" {
  length  = 32
  special = false
}

resource "vault_kv_secret_v2" "roster" {
  mount = "secret"
  name  = "krg-prod/roster"
  data_json = jsonencode({
    db_password    = random_password.roster_db.result
    session_secret = random_password.roster_session.result
  })
}

resource "vault_kv_secret_v2" "roster_ldap" {
  mount = "secret"
  name  = "krg-prod/roster-ldap"
  data_json = jsonencode({
    bind_password = random_password.roster_ldap.result
  })
}
